10 'Programma zet VIDEOWRITER-bestanden over op een MS(X)DOS disk
20 'Peter van Overbeek, 1992
30 A=&HFB21:A$="VIDEOWRITER-":B$=CHR$(34)
40 IFPEEK(A)<2THENPRINT"Voor dit programma zijn twee disk-drives nodig!":END
50 SCREEN0:WIDTH80:KEYOFF:DEFINTA-Z:A=&HD000:B=&H9000:R=&HD062
60 V$="^`'***„*ƒ… ***‰ˆŠ‚‹Œ¡*”*“•¢*–—£"
70 PRINTTAB(30);"VW2MSX.BAS":GOSUB560
80 PRINT"Dit programma zet NMS 3030 "A$"bestanden over op een PC- of MSX-disk."
90 PRINT:PRINT"Zet de "A$"disk in drive A en een PC- of MSX-disk in drive B."
100 LOCATE,6,0:IFPEEK(R)<>80THENGOSUB620
110 PRINTTAB(21)"¯¯¯¯   Druk een toets   ®®®® ";:I$=INPUT$(1)
120 T=0:GOSUB580:CLS:N=PEEK(B+&HBC):IF N>53 THEN X=1:GOTO 610
130 PRINT"Deze "A$"disk bevat de volgende"N"bestand(en) en aantal pagina's."
140 GOSUB 560:PRINT" 1. ¯¯¯¯  STOPPEN  ®®®®":FORF=1TON:LOCATE27*(F\18),FMOD18+2
150 D=B+198+F*64:IFPEEK(D-6)<>1THENGOTO170
160 PRINTUSING"##.";F+1;:GOSUB570:PRINTUSING"##";PEEK(D-5);:NEXT
170 LOCATE0,20:GOSUB560
180 LOCATE4,21:PRINT"Kies het nummer van het over te zetten bestand ";
190 INPUTN$:N=VAL(N$):IFN=1THENGOTO450
200 IFN>F ORN<1THENGOTO180
210 INPUT"    Naam voor het bestand op disk B (max. 8 tekens)";E$
220 IFLEN(E$)>8THENE$=LEFT$(E$,8)
230 E$="B:"+E$+".VWX":OPENE$FOROUTPUTAS#1
240 CLS:PRINTA$"bestand "B$;
250 D=B+134+N*64:GOSUB570:PRINTB$;:P=PEEK(D-5):Q=(P-1)\4
260 PRINT" bestaat uit"P"pagina(s).":GOSUB560
270 DIMT(31),S(31),FT(Q),FS(Q)
280 FORI=0TOQ:FT(I)=PEEK(D+20+2*I):FS(I)=PEEK(D+21+2*I):NEXT
290 FORN=0TOQ:FOR M=0TO3:IF4*N+M+1>PTHENGOTO410
300 PRINT:PRINT"¯¯¯¯¯¯ Pagina"4*N+M+1"®®®®®®®"
310 S=FS(N):IFT<>FT(N)THENT=FT(N):GOSUB580
320 D=B+S*256+M*64:G=PEEK(D):H=PEEK(D+1)
330 FORI=0TOH:T(I)=PEEK(D+2*I+2):S(I)=PEEK(D+2*I+3):NEXT
340 FORI=0TOH:IFT(I)<>TTHENT=T(I):GOSUB580
350 D=B+256*S(I):K=255:IFI=HTHENK=G
360 FORJ=0TOK:C=PEEK(D+J)
370 IFC<32THENGOSUB470ELSEIFC>127THENY=1:GOSUB510
380 IFC=92THENC=156
390 IFYTHENPRINTCHR$(C);:PRINT#1,CHR$(C);
400 NEXT J:NEXT I:NEXT M
410 NEXT N:PRINT:GOSUB560
420 CLOSE#1
430 PRINTA$"bestand "B$C$B$" overgezet naar "B$E$B$"."
440 ERASE T,S,FT,FS:GOTO 110
450 CLS:PRINT"VW2MSX.BAS is een programma van Peter van Overbeek."
460 KEYON:END
470 IFC=OTHENY=0:RETURN
480 IFC>25THENC=ASC(MID$("E O  U",C-25,1)):RETURN
490 IFC=21THENC=142ELSEC=42
500 RETURN
510 IFC=150THENC=10:RETURN
520 IFC=255THENC=13:RETURN
530 IFC>145ANDC<179THENC=ASC(MID$(V$,C-145,1)):RETURN
540 IFC=144THENC=34:RETURN
550 C=42:RETURN
560 FORI=0TO79:PRINT"-";:NEXT:RETURN
570 C$="":FORJ=DTOD+19:C$=C$+CHR$(PEEK(J)):NEXT:PRINTC$;:RETURN
580 DEFUSR=A:X=USR(T):IFX=0THENRETURNELSELOCATE20,12
590 IFXAND128THENPRINT"Geen "A$"disk aanwezig in drive A":GOTO100
600 IFXAND16THENPRINT"Disk in drive A is geen "A$"disk!":GOTO100
610 IFXAND15THENPRINT A$"disk bevat verkeerde gegevens!":GOTO100
620 C=17269:FORI=0TO143:READD$:D=VAL("&H"+D$):POKEA+I,D:C=C-D:NEXT
630 IFCTHENPRINT"Fout in DATA!":POKER,0:KEYON:END
640 RETURN
650 DATA 3A,22,FB,26,40,CD,24,00,AF,32,FC,7F,3E,C0,32,FD
660 DATA 7F,21,F8,7F,55,1B,7A,B3,E3,E3,20,F9,CB,7E,28,06
670 DATA 7E,32,F8,F7,18,2A,CD,77,D0,3A,F8,F7,32,FB,7F,36
680 DATA 14,CD,79,D0,06,12,11,00,90,3E,12,90,32,FA,7F,36
690 DATA 82,CD,82,D0,7E,B7,20,02,10,EF,32,F8,F7,CD,77,D0
700 DATA AF,32,FD,7F,3A,C1,FC,26,40,CD,24,00,FB,C9,28,63
710 DATA 29,20,50,65,74,65,72,20,76,61,6E,20,4F,76,65,72
720 DATA 62,65,65,6B,27,39,32,36,04,3A,FF,7F,CB,77,20,F9
730 DATA 7E,C9,3A,FF,7F,87,F0,38,F9,3A,FB,7F,12,13,18,F2